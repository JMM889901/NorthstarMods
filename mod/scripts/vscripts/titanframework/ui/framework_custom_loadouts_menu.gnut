untyped
globalize_all_functions

struct{
    var menu
    array<var> loadoutButtons
    array<var> loadoutInfo
    table<var, array<var> > loadoutPassiveIcons
    table<var, array<var> > loadoutPassiveIconsImages
    table<var, var> LoadoutTitanIcons
} file
void function InitModdedLoadoutsSelection()
{
    AddSubmenu( "SelectCustomLoadoutsSubMenu", $"resource/ui/menus/customloadoutSubMenu.menu", InitModdedLoadoutsSelectionSubMenu )
	AddMenu( "EditCustomTitanLoadoutsMenu", $"resource/ui/menus/framework_loadout_selection.menu", InitModdedLoadoutsSelectionMenu )
    AddMenu( "EditCustomTitanLoadoutMenu", $"resource/ui/menus/edittitanloadout.menu", InitEditCustomTitanLoadoutMenu )
    AddMenu( "FrameworkNoseArtSelectMenu", $"resource/ui/menus/noseartselect.menu", InitFrameworkNoseArtSelectMenu )
    AddMenu( "FrameworkCamoSelectMenu", $"resource/ui/menus/camoselect.menu", InitFrameworkCamoSelectMenu )
}
void function InitModdedLoadoutsSelectionMenu()
{
	file.menu = GetMenu( "EditCustomTitanLoadoutsMenu" )
	var menu = file.menu
    AddMenuEventHandler( menu, eUIEvent.MENU_OPEN, OnFrameworkLoadoutsMenu_Open )
    InitModdedLoadoutsSelectionMenuInternal(menu)

    file.loadoutButtons = GetElementsByClassname( menu, "CustomTitanLoadoutButton")
    foreach(var loadoutButton in file.loadoutButtons)
    {
        RHud_SetText( loadoutButton, "")
        Hud_AddEventHandler( loadoutButton, UIE_GET_FOCUS, UpdateFrameworkTitanLoadout ) //TODO
        Hud_AddEventHandler( loadoutButton, UIE_CLICK, OnFrameworkLoadoutButton_Press )
    }

	AddMenuFooterOption( menu, BUTTON_A, "#A_BUTTON_SELECT" )
	AddMenuFooterOption( menu, BUTTON_B, "#B_BUTTON_BACK", "#BACK" )
    //AddMenuFooterOption( menu, BUTTON_X, "#SHOW_REJECTED_LOADOUTS", "#SHOW_REJECTED_LOADOUTS", showRejectedLoadouts )
}
void function InitModdedLoadoutsSelectionSubMenu()
{
    var menu = GetMenu( "SelectCustomLoadoutsSubMenu" )
    AddMenuEventHandler( menu, eUIEvent.MENU_OPEN, OnFrameworkLoadoutsSubMenu_Open )
    InitModdedLoadoutsSelectionMenuInternal(menu)
    Hud_SetPos( Hud_GetChild(menu, "Button0"), 0, 0)

    var vguiButtonFrame = Hud_GetChild( menu, "backgroundImage" )
	var ruiButtonFrame = Hud_GetRui( vguiButtonFrame )
	RuiSetImage( ruiButtonFrame, "basicImage", $"rui/borders/menu_border_button" )
	RuiSetFloat3( ruiButtonFrame, "basicImageColor", <0,0,0> )
	RuiSetFloat( ruiButtonFrame, "basicImageAlpha", 0.75 )
    file.loadoutButtons = GetElementsByClassname( menu, "CustomTitanLoadoutButton")
    foreach(var loadoutButton in file.loadoutButtons)
    {
        RHud_SetText( loadoutButton, "")
        Hud_AddEventHandler( loadoutButton, UIE_GET_FOCUS, OnFrameworkSubMenuButton_Focus ) //TODO
        Hud_AddEventHandler( loadoutButton, UIE_CLICK, OnFrameworkSubMenuButton_Click ) //Submenu is only used in LTS
    }
}
void function InitModdedLoadoutsSelectionMenuInternal(var menu)
{
	//AddMenuEventHandler( menu, eUIEvent.MENU_CLOSE, OnTitanLoadoutsMenu_Close )

    foreach(var passiveIcon in GetElementsByClassname( menu, "TitanLoadoutPassiveCombinedImageClass"))
    {
        var button = Hud_GetParent(passiveIcon)
        if(!(button in file.loadoutPassiveIcons))
        {
            file.loadoutPassiveIcons[button] <- []
        }
        file.loadoutPassiveIcons[button].append(passiveIcon)
    }

    foreach(var passiveIcon in GetElementsByClassname( menu, "TitanPassiveIconClass"))
    {
       
        var button = Hud_GetParent(passiveIcon)
        if(!(button in file.loadoutPassiveIconsImages))
        {
            file.loadoutPassiveIconsImages[button] <- []
        }
        file.loadoutPassiveIconsImages[button].append(passiveIcon)
    }//Scuffed but i dont want to remake the .menu

    var nextButton = Hud_GetChild(menu, "buttonNext")
    var previousButton = Hud_GetChild(menu, "buttonPrev")
    Hud_AddEventHandler( nextButton, UIE_CLICK, OnFrameworkLoadoutsMenu_Next )
    Hud_AddEventHandler( previousButton, UIE_CLICK, OnFrameworkLoadoutsMenu_Previous )

    var RejectButton = Hud_GetChild(menu, "buttonRejected")
    Hud_AddEventHandler( RejectButton, UIE_CLICK, showRejectedLoadouts )
}
array<frameworkPersistentTitanLoadout> UILoadouts
int UILoadoutIndex = 0
bool UIShowRejectedLoadouts = false//TODO 

void function showRejectedLoadouts(var button)
{
    UIShowRejectedLoadouts = !UIShowRejectedLoadouts
    if(GetMenu( "EditCustomTitanLoadoutsMenu" ) == GetParentMenu(button) )
        OnFrameworkLoadoutsMenu_Open()
    else if(GetMenu( "SelectCustomLoadoutsSubMenu" ) == GetParentMenu(button) )
        OnFrameworkLoadoutsSubMenu_Open()
    Hud_SetText(button, Localize( UIShowRejectedLoadouts ? "#HIDE_REJECTED_LOADOUTS" : "#SHOW_REJECTED_LOADOUTS" ) )
}
void function OnFrameworkLoadoutsMenu_Open()
{
    UI_SetPresentationType( ePresentationType.TITAN )
    //uiSetUsingCustomTitanLoadoutsMenu(true)
    var menu = GetMenu( "EditCustomTitanLoadoutsMenu" )
    UILoadouts = []
    foreach(string loadoutName, frameworkPersistentTitanLoadout loadout in clientframeworkPersistentTitanLoadouts.loadouts )
    {
        if(loadout.response.status == eLoadoutStatus.REJECTED && !UIShowRejectedLoadouts)
        {
            print("Rejected " + loadoutName +" for reason " + loadout.response.reason + " and status " + loadout.response.cause + " with bad value (if applicable) " + loadout.response.badValue)
            continue
        }
        UILoadouts.append(loadout)
    }
    UILoadouts.sort(SortFrameworkTitanLoadouts)
    UILoadoutIndex = 0
    updateFrameworkLoadoutsMenu(menu)
}
void function OnFrameworkLoadoutsSubMenu_Open()
{
    //uiSetUsingCustomTitanLoadoutsMenu(true)
    var menu = GetMenu( "SelectCustomLoadoutsSubMenu" )
    UILoadouts = []
    foreach(string loadoutName, frameworkPersistentTitanLoadout loadout in clientframeworkPersistentTitanLoadouts.loadouts )
    {
        if(loadout.response.status == eLoadoutStatus.REJECTED && !UIShowRejectedLoadouts)
        {
            print("Rejected " + loadoutName +" for reason " + loadout.response.reason + " and status " + loadout.response.cause + " with bad value (if applicable) " + loadout.response.badValue)
            continue
        }
        UILoadouts.append(loadout)
    }
    UILoadouts.sort(SortFrameworkTitanLoadouts)
    UILoadoutIndex = 0
    updateFrameworkLoadoutsMenu(menu)
}
void function OnFrameworkLoadoutsMenu_Next( var button )
{
    var menu = GetParentMenu(button)
    updateFrameworkLoadoutsMenu(menu)
}
bool function FrameworkSelectionMenu_HasNext()
{
    return UILoadoutIndex <= UILoadouts.len()
}
bool function FrameworkSelectionMenu_HasPrevious()
{
    return UILoadoutIndex > file.loadoutButtons.len()
}
void function OnFrameworkLoadoutsMenu_Previous( var button )
{
    var menu = GetParentMenu(button)
    UILoadoutIndex -= file.loadoutButtons.len() * 2
    updateFrameworkLoadoutsMenu(menu)
}

void function updateFrameworkLoadoutsMenu(var menu)
{
    file.loadoutInfo = GetElementsByClassname( menu, "TitanLoadoutInfoClass")
    file.loadoutButtons = GetElementsByClassname( menu, "CustomTitanLoadoutButton")
    for(int i = 0; i < file.loadoutInfo.len(); i++)
    {
        var loadoutInfo = file.loadoutInfo[i]
        var loadoutButton = file.loadoutButtons[i]
        if(UILoadoutIndex < UILoadouts.len())
        {
            frameworkPersistentTitanLoadout loadout = UILoadouts[UILoadoutIndex]
            //Hud_SetText(loadoutButton, loadout.loadoutName)
            loadoutButton.s.loadoutID <- loadout.fileName
            UpdateTitanButtonInfo(loadoutInfo, loadout)
            Hud_SetSelected(loadoutButton, (loadout.fileName == GetCurrentModdedPersistentTitanLoadoutIndex()))
            Hud_SetVisible(loadoutButton, true)
            Hud_SetVisible(loadoutInfo, true)
        }
        else if(UILoadoutIndex == UILoadouts.len() && Hud_GetHudName(menu) == "EditCustomTitanLoadoutsMenu")
        {
            loadoutButton.s.loadoutID <- "CreateNewTitanLoadout"
            Hud_SetVisible(loadoutButton, true)
            Hud_SetVisible(loadoutInfo, true)
            SetCreateTitanButton(loadoutInfo)
        }
        else
        {
            loadoutButton.s.loadoutID <- ""
            Hud_SetVisible(loadoutButton, false)
            Hud_SetVisible(loadoutInfo, false)
        }
        UILoadoutIndex++
    }
    var nextButton = Hud_GetChild(menu, "buttonNext")
    var previousButton = Hud_GetChild(menu, "buttonPrev")
    if(FrameworkSelectionMenu_HasNext())
    {
        Hud_SetVisible(nextButton, true)
        Hud_SetEnabled(nextButton, true)
    }
    else
    {
        Hud_SetVisible(nextButton, false)
        Hud_SetEnabled(nextButton, false)
    }
    if(FrameworkSelectionMenu_HasPrevious())
    {
        Hud_SetVisible(previousButton, true)
        Hud_SetEnabled(previousButton, true)
    }
    else
    {
        Hud_SetVisible(previousButton, false)
        Hud_SetEnabled(previousButton, false)
    }
    Hud_SetFocused( file.loadoutButtons[ 0 ] )
}
void function UpdateTitanButtonInfo(var menu, frameworkPersistentTitanLoadout loadout)
{
    bool titanDoesExist = IsBaseTitan(loadout.titanName) || GetModdedTitanClasses().contains(loadout.titanName)
    var TitanIcon = Hud_GetChild(menu, "TitanIcon")
    var rui = Hud_GetRui( TitanIcon )
    if(titanDoesExist)
    {
        RuiSetImage(rui, "basicImage", GetIconForTitanClass(loadout.titanName))
    }
    else
    {
        RuiSetImage(rui, "basicImage", $"ui/menu/common/dialog_error")
    }

    Hud_SetText(Hud_GetChild(menu, "titanName"), Localize(loadout.titanName))
    Hud_SetText(Hud_GetChild(menu, "loadoutName"), Localize(loadout.loadoutName))//Loadout names shouldnt be localized but just incase
    if(GetModdedTitanPreviewController(loadout.titanName) != null)
    {
        GetModdedTitanPreviewController(loadout.titanName)(file.loadoutPassiveIcons[menu], loadout.fileName)
        return
    }//Because of the funny circular dependency i cant pass the loadout to this, it must be passed by name, i sure do hope this wont cause problems in future
    foreach(var button in file.loadoutPassiveIcons[menu])
    {

        var imagePanel
        if(ModdedTitanPassiveHasCustomAssets(loadout.titanName, Hud_GetScriptID(button)))
        {
            imagePanel = Hud_GetChild(button, "PassiveImageMod")
            Hud_Hide(Hud_GetChild(button, "PassiveImage"))
        }
        else if(ShouldDisplayIfVanguardPassive( loadout.titanName, Hud_GetScriptID(button) ))
        {
            imagePanel = Hud_GetChild(button, "PassiveImage")
            Hud_Hide(Hud_GetChild(button, "PassiveImageMod"))
        }
        else
        {
            Hud_Hide(Hud_GetChild(button, "PassiveImageMod"))
            Hud_Hide(Hud_GetChild(button, "PassiveImage"))
            continue
        }
        if(!titanDoesExist)
        {
            RuiSetImage( Hud_GetRui(imagePanel), "buttonImage",  $"ui/menu/common/dialog_error" )
            continue
        }
        Hud_SetWidth(imagePanel, 85)
        Hud_SetHeight(imagePanel, 85)//Incase modded previews alter the sizes of the buttons
        Hud_SetPos(imagePanel, 0, 0)
        Hud_Show(imagePanel)
        string itemRef = GetTitanLoadoutValue( loadout.loadout, Hud_GetScriptID(button) )
        int itemType = GetModdedTitanLoadoutPassiveTypeByClass(loadout.titanName, Hud_GetScriptID(button))
        //Despite the name GetModdedTitanLoadoutPassiveTypeByClass actually returns for all titans, not just modded ones
        asset image
        if(ItemDefined(itemRef))
            image = GetImage( itemType, itemRef )
        else
            image = $"ui/menu/common/dialog_error"
        RuiSetImage( Hud_GetRui(imagePanel), "buttonImage", image )
    }
}
void function SetCreateTitanButton( var menu )
{
    foreach(var button in file.loadoutPassiveIcons[menu])
    {

        Hud_Hide(Hud_GetChild(button, "PassiveImageMod"))
        Hud_Hide(Hud_GetChild(button, "PassiveImage"))
    }

    Hud_SetText(Hud_GetChild(menu, "loadoutName"), Localize("#CREATE_NEW_TITAN_LOADOUT"))
    Hud_Hide(Hud_GetChild(menu, "TitanIcon"))
    Hud_Hide(Hud_GetChild(menu, "titanName"))


}
void function OnFrameworkLoadoutButton_Press( var button )
{
    if ( !IsFullyConnected() )
        return

    if(button.s.loadoutID == "")
    {
        print("clicked empty button")
        return
    }
    if( button.s.loadoutID == "CreateNewTitanLoadout" )
    {
        button.s.loadoutID = "" //Prevent double presses
        MenuCreateNewTitan(button)
        return
    }
    setModdedTitanLoadoutIndex( expect string(button.s.loadoutID) )
    ClientCommand( "titanFrameworkSelectLoadout " + button.s.loadoutID )
    int index = GetModdedLoadoutNameIndex( expect string(button.s.loadoutID) )
    SetEditLoadout( "titan", index + FRAMEWORK_TITAN_OFFSET )
    RunMenuClientFunction( "SetEditingTitanLoadoutIndex", index + FRAMEWORK_TITAN_OFFSET )

    //TODO Check that server has the loadout and if not send it
    
    //UpdateFrameworkTitanLoadout(button)
    AdvanceMenu( GetMenu( GetTitanEditLoadoutMenu( GetCurrentModdedPersistentTitanLoadout().titanName ) ) )

}
void function OnFrameworkSubMenuButton_Focus( var button )
{
    string id = expect string(button.s.loadoutID)
    if(id == "CreateNewTitanLoadout")
    {
        return//Should never happen
    }
    TitanLoadoutDef loadout = GetModdedPersistentTitanLoadout( id ).loadout
    int index = GetModdedLoadoutNameIndex( id ) + FRAMEWORK_TITAN_OFFSET
    FrameworkLTSCallback_TitanButton_OnFocused(button, index, loadout )
}
void function OnFrameworkSubMenuButton_Click( var button )
{
    
    CloseSubmenu()
    string ref = expect string(button.s.loadoutID)
    int id = GetModdedLoadoutNameIndex( ref ) + FRAMEWORK_TITAN_OFFSET
    setModdedTitanLoadoutIndex( ref )
    DescribeNewTitanLoadoutToServer( GetModdedPersistentTitanLoadout(ref) )
    FrameworkLTSCallback_TitanButton_OnClick(button, id)
}
void function MenuCreateNewTitan(var button)
{
    CreateNewTitanLoadout()
    while(frameworkHelperValues.savingNewLoadout)
    {
        printt("Waiting" )
        WaitFrame() //when the shitty mutex is sus
    }
    var menu = GetParentMenu(button)
    UILoadoutIndex -= file.loadoutButtons.len()
    if(GetMenu( "EditCustomTitanLoadoutsMenu" ) == menu )
        OnFrameworkLoadoutsMenu_Open()
    else if(GetMenu( "SelectCustomLoadoutsSubMenu" ) == menu )
        OnFrameworkLoadoutsSubMenu_Open()
    while(FrameworkSelectionMenu_HasNext())
    {
        OnFrameworkLoadoutsMenu_Next(button)
    }
    return
}

bool function ShouldDisplayIfVanguardPassive( string titanClass, string propertyName )
{
	if ( titanClass == "vanguard" || ModdedTitanHasPassiveSlot(titanClass, propertyName) )
		return true

	if ( propertyName == "passive4" )
		return false

	if ( propertyName == "passive5" )
		return false

	if ( propertyName == "passive6" )
		return false

	return ModdedTitanHasPassiveSlot(titanClass, propertyName)
}